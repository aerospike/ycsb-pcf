buildscript {
	ext {
		springBootVersion = '1.4.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

task buildYCSB(type: Exec) {
	workingDir 'YCSB'
    commandLine 'mvn', 'package', '-DskipTests'
}

task configureYCSB(type: Copy, dependsOn: 'buildYCSB') {
	into('libs')
	File bindingFile = new File('YCSB/bin/bindings.properties')
	bindingFile.eachLine { line ->
	  if (line.length() > 0 && line[0] != '#') {
	  	  def words = line.split(":")
		  def dir = 'YCSB/' + words[0] + '/target'
		  from(dir) {
		     include '*.jar'
		  }
		}
	}
	from('YCSB/core/target') {
	   include '*.jar'
	}
	from('YCSB/binding-parent/datastore-specific-descriptor/target') {
	   include '*.jar'
	}
}

jar {
	baseName = 'ycsb-web'
	version = '0.0.9-SNAPSHOT'
	
     from('src/main/java/com/aerospike/properties') {
        include '*'
        into('com/aerospike/properties') 
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.apache.htrace:htrace-core4:4.1.0-incubating'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.4'
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.4'
    compile 'org.testng:testng:6.1.1'
    compile 'org.hdrhistogram:HdrHistogram:2.1.4'
    compile 'com.aerospike:aerospike-client:3.3.0'
    compile group:'io.pivotal.labs', name:'cf-env', version:'0.0.1'
    
    compile fileTree(dir: "libs", includes: ['*.jar'])
    
    compile 'redis.clients:jedis:2.9.0'
    compile 'org.springframework.cloud:spring-cloud-spring-service-connector'
    compile 'org.springframework.cloud:spring-cloud-cloudfoundry-connector'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
	compile('org.springframework.boot:spring-boot-starter-web')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}
